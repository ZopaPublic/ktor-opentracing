buildscript {
    ext.ktor_version = "1.3.2"
    ext.opentracing_version = "0.33.0"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-library'
}

apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation 'io.github.microutils:kotlin-logging:1.7.7'

    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"

    implementation "io.opentracing:opentracing-api:$opentracing_version"
    implementation "io.opentracing:opentracing-noop:$opentracing_version"
    implementation "io.opentracing:opentracing-util:$opentracing_version"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:0.23"
    testImplementation "io.opentracing:opentracing-mock:$opentracing_version"
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')

}

test {
    useJUnitPlatform()
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

group = "com.zopa"
archivesBaseName = "ktor-opentracing"
version = "0.1.1"

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            pom.project {
                name 'ktor-opentracing'
                packaging 'jar'
                description 'Ktor features for OpenTracing instrumentation.'
                url 'https://github.com/zopaUK/ktor-opentracing'

                scm {
                    connection 'scm:git:git://github.com/zopaUK/ktor-opentracing.git'
                    developerConnection 'scm:git:ssh://github.com/zopaUK/ktor-opentracing.git'
                    url 'https://github.com/zopaUK/ktor-opentracing'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'fstien'
                        name 'Francois Stiennon'
                        email 'francois.stiennon@gmail.com'
                    }
                }
            }
        }
    }
}
